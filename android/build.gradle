apply plugin: 'com.android.library'
apply plugin: 'maven-publish'

archivesBaseName = 'jmini3d-android'

dependencies {
    api project(':core')
}

android {
    namespace 'jmini3d.android'
    compileSdk 33
    buildToolsVersion = '33.0.1'

    defaultConfig {
        targetSdkVersion 33
        // Android 2.2 (=8) has VBO problems for versions < 2.3 we will use OpenGL ES 1.1 -->
        minSdkVersion 9
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_11
        targetCompatibility JavaVersion.VERSION_11
    }

    publishing {
        singleVariant("release") {
            withSourcesJar()
        }
    }
}

task copyResources(type: Copy)

copyResources {
    from '../core/shaders'
    into 'src/main/res/raw'
    include('**/*')
}

preBuild.dependsOn copyResources

publishing {
    publications {
        jmini3dAndroid(MavenPublication) {
            artifactId = 'jmini3d-android'

            afterEvaluate {
                from components.release
            }
        }
    }
}
